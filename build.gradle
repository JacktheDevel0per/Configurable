plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.9-SNAPSHOT' apply false


	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version '9d21b334a7'


}

preprocess {

	def mc119      = createNode('1.19.4'  , 1_19_04, 'yarn')
	def mc120_4    = createNode('1.20.4'  , 1_20_04, 'yarn')
	def mc121_1    = createNode('1.21.1'  , 1_21_01, 'yarn')
	def mc121_4    = createNode('1.21.4'  , 1_21_04, 'yarn')

	mc121_4.link(mc121_1, null)
	mc121_1.link(mc120_4, null)
	mc120_4.link(mc119, null)


}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
